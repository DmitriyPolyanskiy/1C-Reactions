
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтрокиДерева = Дерево.ПолучитьЭлементы();
	
	Для Каждого ОбщийМодуль Из Метаданные.ОбщиеМодули Цикл
		
		Если Не СтрНайти(ОбщийМодуль.Имя, "РеакцииКлиентСервер") Тогда
			Продолжить;
		КонецЕсли;
		
		МодульНабора  = ОбщегоНазначения.ОбщийМодуль(ОбщийМодуль.Имя);
		РеакцииНабора = МодульНабора.ОписаниеРеакцийНабора();
		
		Набор = СтрокиДерева.Добавить();
		Набор.Представление = ОбщийМодуль.РасширениеКонфигурации().Синоним;
		СтрокиНабора = Набор.ПолучитьЭлементы();
		
		Для Каждого ИмяИПредставление Из РеакцииНабора Цикл
			
			Строка = СтрокиНабора.Добавить();
			Строка.Реакция       = ИмяИПредставление.Ключ;
			Строка.Представление = ИмяИПредставление.Значение;
			Строка.Картинка      = БиблиотекаКартинок[ИмяИПредставление.Ключ];
			
		КонецЦикла;
		
	КонецЦикла;
	
	НаборПользовательских = СтрокиДерева.Добавить();
	НаборПользовательских.Представление = нСтр("ru='Пользовательские реакции'", "ru");
	СтрокиНабора = НаборПользовательских.ПолучитьЭлементы();
	
	ПользовательскиеРеакции = Справочники.РКЦ_Реакции.Выбрать();
	Пока ПользовательскиеРеакции.Следующий() Цикл
		
		Строка = СтрокиНабора.Добавить();
		Строка.Реакция       = "П." + ПользовательскиеРеакции.Код;
		Строка.Представление = ПользовательскиеРеакции.Наименование;
		Строка.Картинка      = ПользовательскиеРеакции.Картинка.Получить();
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Отреагировать(Команда)
	ВыбратьРеакцию();
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ВыбратьРеакцию();
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьРеакцию()
	
	ТекущаяРеакция = Дерево.НайтиПоИдентификатору(Элементы.Дерево.ТекущаяСтрока);
	
	Если ТекущаяРеакция.Свойство("Реакция")
		И ЗначениеЗаполнено(ТекущаяРеакция.Реакция) Тогда
		
		Закрыть(ТекущаяРеакция.Реакция);
		
	КонецЕсли;
	
КонецПроцедуры
