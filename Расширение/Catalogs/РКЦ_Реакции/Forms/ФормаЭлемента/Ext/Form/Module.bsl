
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Хранилище = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Ссылка, "Картинка");
	Картинка = Хранилище.Получить();
	
	НавигационнаяСсылкаКартинки = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "Картинка");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Картинка.Вид = ВидКартинки.Пустая Тогда
		ТекстСообщения = нСтр("ru='Картинка должна быть заполнена'", "ru");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "Картинка", , Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.Картинка = Новый ХранилищеЗначения(Картинка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Загрузить(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("Загрузить_Продолжение", ЭтотОбъект);
	
	ТекстВопроса = нСтр("ru='После загрузки элемент будет записан'", "ru");
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
	
КонецПроцедуры

&НаКлиенте
Процедура Загрузить_Продолжение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("Загрузить_Завершение", ЭтотОбъект);
		
		Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		Диалог.МножественныйВыбор = Ложь;
		Диалог.Показать(ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Загрузить_Завершение(ПутьКФайлу, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ПутьКФайлу) Тогда
		
		Картинка = Новый Картинка(ПутьКФайлу[0]);
		
		Если Записать() Тогда
			НавигационнаяСсылкаКартинки = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "Картинка");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("Сохранить_Завершение", ЭтотОбъект);
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.Фильтр = Картинка.ФильтрИменФайлов();
	Диалог.Показать(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить_Завершение(ПутьКФайлу, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ПутьКФайлу) Тогда
		НавигационнаяСсылкаКартинки.Записать(ПутьКФайлу[0]);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти