
#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

#Область СерверныеОповещения

Процедура ПриПолученииСерверногоОповещения(ИмяОповещения, Результат) Экспорт
	
	Если ИмяОповещения = "РКЦ_Реакции.РеакцииНаДокументыПользователя" Тогда
		ТекстСообщения = нСтр("ru='[ИмяПользователя] отреагировал ""[ПредставлениеРеакции]"" на Ваш документ [ПредставлениеОбъекта]'", "ru");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ТекстСообщения, Результат);
		ПоказатьОповещениеПользователя(
			нСтр("ru='Новая реакция!'", "ru"),
			Результат.URL,
			ТекстСообщения,
			Результат.Картинка,
			СтатусОповещенияПользователя.Важное
		);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеКоманды

Процедура Отреагировать(ПараметрыКоманды, ПараметрыВыполнения) Экспорт
	
	ОписаниеКоманды = ПараметрыВыполнения.ОписаниеКоманды;
	ОтреагироватьНаКлиенте(ПараметрыКоманды, ОписаниеКоманды.Идентификатор);
	
КонецПроцедуры

Процедура УдалитьРеакцию(ПараметрыКоманды, ПараметрыВыполнения) Экспорт
	
	РКЦ_РеакцииВызовСервера.УдалитьРеакциюНаСервере(ПараметрыКоманды);
	
	ОповеститьОбИзменении(ПараметрыКоманды);
	
КонецПроцедуры

Процедура ВыбратьИзВсехРеакций(ПараметрыКоманды, ПараметрыВыполнения) Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьИзВсехРеакций_Завершение", ЭтотОбъект, ПараметрыКоманды);
	ОткрытьФорму("ОбщаяФорма.РКЦ_ВсеРеакции", , ПараметрыВыполнения.Форма, , , , ОписаниеОповещения);
	
КонецПроцедуры

Процедура ВыбратьИзВсехРеакций_Завершение(Результат, ПараметрыКоманды) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		ОтреагироватьНаКлиенте(ПараметрыКоманды, Результат);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОтреагироватьНаКлиенте(Объект, КодРеакции)
	
	РКЦ_РеакцииВызовСервера.ОтреагироватьНаСервере(Объект, КодРеакции);
	
	ОповеститьОбИзменении(Объект);
	
КонецПроцедуры

#КонецОбласти
