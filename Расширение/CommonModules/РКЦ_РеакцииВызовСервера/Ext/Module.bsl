
Процедура ОтреагироватьНаСервере(Объект, КодРеакции) Экспорт
	
	//TODO: уменьшить кол-во обращений к серверу БД
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	ЭтоПользовательскаяРеакция = СтрНачинаетсяС(КодРеакции, "П.");
	
	Если ЭтоПользовательскаяРеакция Тогда
		Реакция = Справочники.РКЦ_Реакции.НайтиПоКоду(Сред(КодРеакции, 3), Истина);
	Иначе
		Реакция = КодРеакции;
	КонецЕсли;
	
	//Попытка
	
	МенеджерЗаписи = РегистрыСведений.РКЦ_РеакцииОбъектов.СоздатьМенеджерЗаписи();
	
	МенеджерЗаписи.ИдентификаторОбъектаМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(Объект));
	МенеджерЗаписи.Идентификатор = Объект.УникальныйИдентификатор();
	МенеджерЗаписи.Пользователь  = ТекущийПользователь;
	МенеджерЗаписи.Реакция       = Реакция;
	
	МенеджерЗаписи.Записать();
	
	//Исключение
	//	
	//	ЗаписьЖурналаРегистрации(
	//		"РКЦ_Реакции.Отреагировать на сервере",
	//		УровеньЖурналаРегистрации.Ошибка, , ,
	//		нСтр("ru='Ошибка записи реакции в регистр сведений.'", "ru") + Символы.ПС +
	//		ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())
	//	);
	//	ВызватьИсключение;
	//	
	//КонецПопытки;
	
	СвойстваОбъекта = РКЦ_РеакцииПовтИсп.СвойстваОбъектовСРеакциями()[Объект.Метаданные()];
	Если Не ЗначениеЗаполнено(СвойстваОбъекта) Тогда
		Возврат;
	КонецЕсли;
	ИмяРеквизита = СвойстваОбъекта.РеквизитПолучательОповещения;
	Если Не ЗначениеЗаполнено(ИмяРеквизита) Тогда
		Возврат;
	КонецЕсли;
	
	Картинка = ?(ТипЗнч(Реакция) = Тип("Строка"),
		БиблиотекаКартинок[Реакция],
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Реакция, "Картинка").Получить()
	);
	
	БольшаяКартинка = Картинка;
	
	Если Картинка.НаборВариантов Тогда
		
		Варианты = Новый Соответствие();
		Варианты.Вставить("screenDensity", "udpi");
		
		ДвоичныеДанныеБольшой = Картинка.ПолучитьДвоичныеДанные(Ложь, Варианты);
		
		Если ДвоичныеДанныеБольшой <> Неопределено Тогда
			БольшаяКартинка = Новый Картинка(ДвоичныеДанныеБольшой);
		КонецЕсли;
		
	Иначе
		
		Обрабатываемая = Новый ОбрабатываемаяКартинка(Картинка);
		
		Обрабатываемая.УстановитьРазмер(64, 64);
		
		БольшаяКартинка = Обрабатываемая.ПолучитьКартинку();
		
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ИмяПользователя", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущийПользователь, "Наименование"));
	Результат.Вставить("Картинка",        БольшаяКартинка);
	Результат.Вставить("Представление",   Строка(Объект));
	Результат.Вставить("URL",             ПолучитьНавигационнуюСсылку(Объект));
	
	Адресат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект, ИмяРеквизита);
	
	Адресаты = Новый Соответствие;
	Адресаты.Вставить(
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Адресат, "ИдентификаторПользователяИБ"),
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("*") // Все сеансы этого пользователя, видимо
	);
	
	СерверныеОповещения.ОтправитьСерверноеОповещение(
		"РКЦ_Реакции.РеакцииНаДокументыПользователя",
		Результат,
		Адресаты
	);
	
КонецПроцедуры

Процедура УдалитьРеакциюНаСервере(Объект) Экспорт
	
	МенеджерЗаписи = РегистрыСведений.РКЦ_РеакцииОбъектов.СоздатьМенеджерЗаписи();
	
	МенеджерЗаписи.ИдентификаторОбъектаМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(Объект));
	МенеджерЗаписи.Идентификатор = Объект.УникальныйИдентификатор();
	МенеджерЗаписи.Пользователь  = Пользователи.ТекущийПользователь();
	
	МенеджерЗаписи.Удалить();
	
КонецПроцедуры
