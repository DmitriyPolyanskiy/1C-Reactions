
Процедура ОтреагироватьНаСервере(Объект, КодРеакции) Экспорт
	
	МетаданныеОбъекта = Объект.Метаданные();
	НастройкиРеакций = Новый Структура;
	Если Не РКЦ_Реакции.НастройкиРеакцийОбъекта(МетаданныеОбъекта.ПолноеИмя(), НастройкиРеакций) Тогда
		Возврат;
	КонецЕсли;
	
	//TODO: уменьшить кол-во обращений к серверу БД
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	ЭтоПользовательскаяРеакция = СтрНачинаетсяС(КодРеакции, "П.");
	Если ЭтоПользовательскаяРеакция Тогда
		Реакция = Справочники.РКЦ_Реакции.НайтиПоКоду(Сред(КодРеакции, 3), Истина);
	Иначе
		Реакция = КодРеакции;
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.РКЦ_РеакцииОбъектов.СоздатьМенеджерЗаписи();
	
	МенеджерЗаписи.ИдентификаторОбъектаМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(МетаданныеОбъекта);
	МенеджерЗаписи.Идентификатор = Объект.УникальныйИдентификатор();
	МенеджерЗаписи.Пользователь  = ТекущийПользователь;
	МенеджерЗаписи.Реакция       = Реакция;
	
	МенеджерЗаписи.Записать();
	
	// Отравка оповещения
	Если ПустаяСтрока(НастройкиРеакций.РеквизитПолучательОповещения) Тогда
		Возврат;
	КонецЕсли;
	
	// Определение адресата
	Попытка
		Адресат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект, НастройкиРеакций.РеквизитПолучательОповещения);
	Исключение
		ЗаписьЖурналаРегистрации(
			"Реакции.Отреагировать на сервере.Определение адресата уведомления",
			УровеньЖурналаРегистрации.Ошибка,
			МетаданныеОбъекта,
			НастройкиРеакций.РеквизитПолучательОповещения,
			нСтр("ru='Ошибка получения значения реквизита с адресатом объекта реакции.'", "ru") + Символы.ПС +
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())
		);
		Возврат;
	КонецПопытки;
	
	Адресаты = Новый Соответствие;
	Адресаты.Вставить(
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Адресат, "ИдентификаторПользователяИБ"),
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("*") // Все сеансы этого пользователя, видимо
	);
	
	// Формирование результата
	Картинка = ?(ТипЗнч(Реакция) = Тип("Строка"),
		БиблиотекаКартинок[Реакция],
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Реакция, "Картинка").Получить()
	);
	
	// В оповещениях картинка должна быть примерно 64*64
	// Автоматически к этому размеру не приводится
	БольшаяКартинка = Картинка; 
	Если Картинка.НаборВариантов Тогда
		Варианты = Новый Соответствие();
		Варианты.Вставить("screenDensity", "udpi");// TODO: Заменить тег на wigth или height
		
		ДвоичныеДанныеБольшой = Картинка.ПолучитьДвоичныеДанные(Ложь, Варианты);
		Если ДвоичныеДанныеБольшой <> Неопределено Тогда
			БольшаяКартинка = Новый Картинка(ДвоичныеДанныеБольшой);
		КонецЕсли;
	Иначе
		Обрабатываемая = Новый ОбрабатываемаяКартинка(Картинка);
		Обрабатываемая.УстановитьРазмер(64, 64);
		БольшаяКартинка = Обрабатываемая.ПолучитьКартинку();
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ИмяПользователя", Строка(ТекущийПользователь));
	Результат.Вставить("Картинка",        БольшаяКартинка);
	Результат.Вставить("ПредставлениеРеакции", Строка(Реакция));
	Результат.Вставить("ПредставлениеОбъекта", Строка(Объект));
	Результат.Вставить("URL",             ПолучитьНавигационнуюСсылку(Объект));
	
	СерверныеОповещения.ОтправитьСерверноеОповещение(
		"РКЦ_Реакции.РеакцииНаДокументыПользователя",
		Результат,
		Адресаты
	);
	
КонецПроцедуры

Процедура УдалитьРеакциюНаСервере(Объект) Экспорт
	
	МенеджерЗаписи = РегистрыСведений.РКЦ_РеакцииОбъектов.СоздатьМенеджерЗаписи();
	
	МенеджерЗаписи.ИдентификаторОбъектаМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(Объект));
	МенеджерЗаписи.Идентификатор = Объект.УникальныйИдентификатор();
	МенеджерЗаписи.Пользователь  = Пользователи.ТекущийПользователь();
	
	МенеджерЗаписи.Удалить();
	
КонецПроцедуры
