
&НаСервереБезКонтекста
Процедура ДСПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РеакцииОбъектов.Объект КАК Объект,
	|	РеакцииОбъектов.Реакция КАК Реакция,
	|	КОЛИЧЕСТВО(РеакцииОбъектов.Реакция) КАК Число,
	|	МАКСИМУМ(РеакцииОбъектов.Пользователь = &ТекущийПользователь) КАК Наша
	|ПОМЕСТИТЬ РеакцииОбъекта
	|ИЗ
	|	РегистрСведений.РКЦ_РеакцииОбъектов КАК РеакцииОбъектов
	|ГДЕ
	|	РеакцииОбъектов.Объект В(&Объекты)
	|
	|СГРУППИРОВАТЬ ПО
	|	РеакцииОбъектов.Объект,
	|	РеакцииОбъектов.Реакция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеакцииОбъекта.Объект КАК Объект,
	|	РеакцииОбъекта.Число КАК Число,
	|	РеакцииОбъекта.Наша КАК Наша,
	|	РеакцииОбъекта.Реакция КАК Реакция,
	|	РКЦ_Реакции.Картинка КАК Картинка,
	|	РКЦ_Реакции.Наименование КАК Наименование
	|ИЗ
	|	РеакцииОбъекта КАК РеакцииОбъекта
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РКЦ_Реакции КАК РКЦ_Реакции
	|		ПО РеакцииОбъекта.Реакция = РКЦ_Реакции.Ссылка
	|ИТОГИ ПО
	|	Объект";
	
	Запрос.УстановитьПараметр("Объекты", Строки.ПолучитьКлючи());
	Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.ТекущийПользователь());
	
	ВыборкаОбъектов = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаОбъектов.Следующий() Цикл
		СтрокаСписка = Строки[ВыборкаОбъектов.Объект];
		
		СписокРеакций = Новый СписокЗначений;
		ВыборкаРеакций = ВыборкаОбъектов.Выбрать();
		Пока ВыборкаРеакций.Следующий() Цикл
			СписокРеакций.Добавить(
				ВыборкаРеакций.Наименование,
				ВыборкаРеакций.Число,
				ВыборкаРеакций.Наша,
				ВыборкаРеакций.Картинка.Получить()
			);
		КонецЦикла;
		СтрокаСписка.Данные["РКЦ_Реакции"] = СписокРеакций;
		
	КонецЦикла;
	
КонецПроцедуры
